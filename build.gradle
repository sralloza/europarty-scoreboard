plugins {
    id 'application'
    id 'groovy'
    id 'java'
    id 'io.freefair.lombok' version '6.4.1'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'

        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true

        showStandardStreams false
    }
}

group 'es.sralloza'
version '1.0-SNAPSHOT'
mainClassName = System.getenv("EUROPARTY_MAIN_CLASS") ?: "ScoreboardCreationByForm"

repositories {
    mavenCentral()
}

dependencies {
    // Gradle
    implementation 'org.codehaus.groovy:groovy-all:3.0.10'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.5.1'

    // Utils
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.inject:guice:5.1.0'
    implementation 'com.typesafe:config:1.4.2'
    implementation 'org.projectlombok:lombok:1.18.24'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.0'

    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.3', {
        transitive = false
    }
    implementation 'org.seleniumhq.selenium:selenium-api:4.1.3'
    implementation 'org.seleniumhq.selenium:selenium-support:4.1.3'
    implementation 'org.seleniumhq.selenium:selenium-firefox-driver:4.1.3'

    // Google Sheets
    implementation 'com.google.api-client:google-api-client:1.34.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20220411-1.32.1'

    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.tersesystems.logback:logback-classic:1.0.2'
    implementation 'com.tersesystems.logback:logback-structured-config:1.0.2'
    implementation 'com.tersesystems.logback:logback-typesafe-config:1.0.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.1.1'
}

test {
    useJUnitPlatform()
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    manifest {
        attributes 'Main-Class': mainClassName
    }

    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
